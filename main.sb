' **********PROGRAM PROPERTIES**************
' *************************************************
' GAME PROPERTIES
FPS = 60

' BALLOON PROPERTIES
BALLOON_WIDTH = 30
BALLOON_HEIGHT = 30
BALLOON_SPEED = 2 'Pixels Per Frame
SECS_TILL_BALL_CHANGES_DIR = 1 'Seconds until the balloon changes direction
FRAMES_TILL_BALL_CHANGES_DIR = SECS_TILL_BALL_CHANGES_DIR * FPS

' PLAYER PROPERTIES
PLAYER_WIDTH = 60
PLAYER_HEIGHT = 30
PLAYER_BARREL_WIDTH = 20
PLAYER_BARREL_HEIGHT = 15

' WINDOW PROPERTIES
WNDOW_PADDING = 10

'**********MAIN GAME LOGIC*************
'*******************************************
createBalloon()
createPlayer()

currStep = FRAMES_TILL_BALL_CHANGES_DIR
gameWon = "False"
While gameWon = "False"
  
  FrameRate()
  
  currStep = currStep + 1
  If (currStep >= FRAMES_TILL_BALL_CHANGES_DIR) Or (balloonY <= WNDOW_PADDING) or (balloonY >= (gh - WNDOW_PADDING - BALLOON_HEIGHT)) Then
    changeYDirection()
    currStep = 0
  EndIf
  
  balloonY = balloonY + balloonDeltaY
  balloonX = balloonX + balloonDeltaX
  Shapes.Move(balloon, balloonX, balloonY)  
EndWhile

' **********SUBROUTINES***********
'*************************************
'   Create a balloon using balloon properties to determine width and height,
' then position it at a random position in the left quarter of the game window.
Sub createBalloon
  GraphicsWindow.BrushColor = "YellowGreen"
  GraphicsWindow.PenColor = "LimeGreen"
  balloon = Shapes.AddEllipse(BALLOON_WIDTH, BALLOON_HEIGHT)
  
  gw = GraphicsWindow.Width
  balloonX = Math.GetRandomNumber(gw / 4 + WNDOW_PADDING)
  
  gh = GraphicsWindow.Height
  balloonY = Math.GetRandomNumber(gh - WNDOW_PADDING * 2) + WNDOW_PADDING
  
  Shapes.Move(balloon, balloonX, balloonY)
EndSub

' Create the player at the center right of the game window.
Sub createPlayer
  GraphicsWindow.BrushColor = "Black"
  GraphicsWindow.PenColor = "Black"
  playerBody = Shapes.AddRectangle(PLAYER_WIDTH, PLAYER_HEIGHT)
  playerBarrel = Shapes.AddRectangle(PLAYER_BARREL_WIDTH, PLAYER_BARREL_HEIGHT)
  
  ' Set x coordinate to far right of game window, with a little padding.
  gw = GraphicsWindow.Width
  playerBodyX = gw - WNDOW_PADDING - PLAYER_WIDTH
  playerBarrelX = playerBodyX - PLAYER_BARREL_WIDTH
  
  ' Set y coordinate to middle of screen.
  gh = GraphicsWindow.Height
  playerBodyY = (gh / 2) - (PLAYER_HEIGHT / 2)
  playerBarrelY = playerBodyY + (PLAYER_HEIGHT / 2) - (PLAYER_BARREL_HEIGHT / 2)
  
  Shapes.Move(playerBody, playerBodyX, playerBodyY)
  Shapes.Move(playerBarrel, playerBarrelX, playerBarrelY)
EndSub

'  Set the "sign" variable to equal either 1 or -1, with a 50% liklihood of either
Sub getRandSign
  x = Math.GetRandomNumber(2)
  If x = 1 Then
    sign = 1
  Else
    sign = -1
  EndIf
EndSub

' Delay each render frame such that only a set number of frames occur each second, as defined
' by the "FPS" property.
' 
' This is so that object speeds are comparable from computer to computer, rather than
' determined by CPU speed.
' 
' Modified from a function used in: http://smallbasic.com/program/?PMD031-0
Sub FrameRate
  millisecondsPerFrame = 1000 / FPS
  delay = millisecondsPerFrame - (Clock.ElapsedMilliseconds - start)
  If delay > 0 Then
    Program.Delay(delay)
  EndIf
  start = Clock.ElapsedMilliseconds
EndSub

'  Randomly change (50 / 50)  the vertical direction the balloon is moving in. (up or down)
' If the balloon is within a tolerance (set with the WINDOW_PADDING property) of
' the top of the game window, it will "bounce" rather than randomly select the dirction.
Sub changeYDirection
  getRandSign()    
  gh = GraphicsWindow.Height
  ' "Bounce" the balloon if it's too close to the top of the game window.
  If (balloonY <= WNDOW_PADDING) Then
    sign = 1
  EndIf
  ' "Bounce" the balloon if it's too close to the bottom of the game window.
  If (balloonY >= (gh - WNDOW_PADDING)) Then
    sign = -1
  EndIf  
  
  balloonDeltaY = sign * BALLOON_SPEED
EndSub


' ******************\/ Code Dump*******************

Sub changeXDirection
  getRandSign()  
  gh = GraphicsWindow.Height
  
  If (balloonX <= WNDOW_PADDING) Then
    sign = 1
  EndIf
  
  If (balloonX >= (gw / 4 - WNDOW_PADDING)) Then
    sign = -1
  EndIf  
  
  balloonDeltaX = sign * BALLOON_SPEED
EndSub